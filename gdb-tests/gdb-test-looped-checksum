
define print_separator
  printf "\n********************************************************************************\n" 
end


define test_failed
  # if it is not catched by any pass conditions, then it must be fail
  print_separator
  printf "FAILED the test\n"
  print_separator

  # exiting with a specific exit code newer kernels can have 32bit exit code so it
  # will not be mistaken by any other exit codes which could happen on communication
  # or other errors
  quit 150
end


define test_expected
  printf "expected=%08X actual=%08X\n", $arg0, sum

  if (sum == $arg0)
    print_separator
    print "PASSED the test\n"
    print_separator

    # exiting with a specific exit code newer kernels can have 32bit exit code so it
    # will not be mistaken by any other exit codes which could happen on communication
    # or other errors

    quit 149   
  else
    test_failed
  end
end


target remote localhost:3333
load miv-rv32imaf-dct.elf
break display_total_sum
continue

# Arrived at the display_total_sum breakpoint 
# Check for all valid checkshums depending on the iterations proccessed

printf "Test was iterated=%d times ", current_iteration

if (current_configuration & 0x4)
    #hard float

    if (current_iteration == 1200)
        test_expected 0x2c94b40c
    end

    if (current_iteration == 600)
        test_expected 0x783b7b38
    end

    if (current_iteration == 400)
        test_expected 0xa57cfcd0
    end

    if (current_iteration == 200)
        test_expected 0xd2be7e68
    end

    if (current_iteration == 100)
        test_expected 0x695f3f34
    end

    if (current_iteration == 50)
        test_expected 0x34af9f9a
    end

    if (current_iteration == 10)
        test_expected 0x0a898652
    end
else
    # soft float implementation
    
    if (current_iteration == 10)
        test_expected 0x00858f8c
    end

end

print_separator
printf "test failing because you use UNSUPORTED iterations.\n"
print_separator

test_failed